###**
Application layout
==================

You can [make Unpoly aware](/up.layout.config) of fixed elements in your
layout, such as navigation bars or headers. Unpoly will respect these sticky
elements when [revealing elements](/up.reveal) or [opening a modal dialog](/a-up-modal).

This modules also contains functions to programmatically [scroll a viewport](/up.scroll)
or [reveal an element within its viewport](/up.reveal).

Bootstrap integration
---------------------

When using Bootstrap integration (`unpoly-bootstrap3.js` and `unpoly-bootstrap3.css`)
Unpoly will automatically be aware of sticky Bootstrap components such as
[fixed navbar](https://getbootstrap.com/examples/navbar-fixed-top/).

@class up.layout
###
up.layout = do ->

  u = up.util
  e = up.element

  ###**
  Configures the application layout.

  @property up.layout.config
  @param {Array} [config.viewports]
    An array of CSS selectors that find viewports
    (containers that scroll their contents).
  @param {Array} [config.fixedTop]
    An array of CSS selectors that find elements fixed to the
    top edge of the screen (using `position: fixed`).
    See [`[up-fixed="top"]`](/up-fixed-top) for details.
  @param {Array} [config.fixedBottom]
    An array of CSS selectors that find elements fixed to the
    bottom edge of the screen (using `position: fixed`).
    See [`[up-fixed="bottom"]`](/up-fixed-bottom) for details.
  @param {Array} [config.anchoredRight]
    An array of CSS selectors that find elements anchored to the
    right edge of the screen (using `right:0` with `position: fixed` or `position: absolute`).
    See [`[up-anchored="right"]`](/up-anchored-right) for details.
  @param {number} [config.snap=50]
    When [revealing](/up.reveal) elements, Unpoly will scroll an viewport
    to the top when the revealed element is closer to the top than `config.snap`.
  @stable
  ###
  config = u.config
    duration: 0
    viewports: ['.up-modal-viewport', '[up-viewport]', '[up-fixed]']
    fixedTop: ['[up-fixed~=top]']
    fixedBottom: ['[up-fixed~=bottom]']
    anchoredRight: ['[up-anchored~=right]', '[up-fixed~=top]', '[up-fixed~=bottom]', '[up-fixed~=right]']
    revealSnap: 50
    revealPadding: 0,
    scrollSpeed: 1

  lastScrollTops = new up.Cache
    size: 30,
    key: up.history.normalizeUrl

  scrollingTracker = new up.MotionTracker('scrolling')

  reset = ->
    config.reset()
    lastScrollTops.clear()
    scrollingTracker.reset()

  ###**
  Scrolls the given viewport to the given Y-position.

  A "viewport" is an element that has scrollbars, e.g. `<body>` or
  a container with `overflow-x: scroll`.

  \#\#\# Example

  This will scroll a `<div class="main">...</div>` to a Y-position of 100 pixels:

      up.scroll('.main', 100);

  \#\#\# Animating the scrolling motion

  The scrolling can (optionally) be animated.

      up.scroll('.main', 100, {
        easing: 'swing',
        duration: 250
      });

  If the given viewport is already in a scroll animation when `up.scroll()`
  is called a second time, the previous animation will instantly jump to the
  last frame before the next animation is started.

  @function up.scroll
  @param {string|Element|jQuery} viewport
    The container element to scroll.
  @param {number} scrollPos
    The absolute number of pixels to set the scroll position to.
  @param {string}[options.behavior='auto']
    When set to `'auto'`, this will immediately scroll to the new position.

    When set to `'smooth'`, this will scroll smoothly to the new position.
  @return {Promise}
    A promise that will be fulfilled when the scrolling ends.
  @experimental
  ###
  scroll = (viewport, scrollTop, options) ->
    motion = new up.ScrollMotion(viewport, scrollTop, options)
    scrollingTracker.startMotion(viewport, motion, options)

  ###**
  Finishes scrolling animations in the given element, its ancestors or its descendants.

  @function up.layout.finishScrolling
  @param {string|Element|jQuery}
  @return {Promise}
  @internal
  ###
  finishScrolling = (element) ->
    # Don't emit expensive events if no animation can be running anyway
    return Promise.resolve() unless up.motion.isEnabled()

    scrollable = viewportOf(element)
    scrollingTracker.finish(scrollable)

  ###**
  @function up.layout.anchoredRight
  @internal
  ###
  anchoredRight = ->
    selector = config.anchoredRight.join(',')
    e.all(selector)

  ###**
  @function measureObstruction
  @return {Object}
  @internal
  ###
  measureObstruction = (viewportHeight) ->
    composeHeight = (obstructor, distanceFromEdgeProps) ->
      distanceFromEdge = u.sum(distanceFromEdgeProps, (prop) -> u.readComputedStyleNumber(obstructor, prop)) || 0
      distanceFromEdge + obstructor.offsetHeight

    measureTopObstructor = (obstructor) ->
      composeHeight(obstructor, ['top', 'margin-top'])

    measureBottomObstructor = (obstructor) ->
      composeHeight(obstructor, ['bottom', 'margin-bottom'])

    topObstructors = e.all(config.fixedTop.join(', '))
    bottomObstructors = e.all(config.fixedBottom.join(', '))

    topObstructions = u.map(topObstructors, measureTopObstructor)
    bottomObstructions = u.map(bottomObstructors, measureBottomObstructor)

    top: Math.max(0, topObstructions...)
    bottom: Math.max(0, bottomObstructions...)

  ###**
  Scroll's the given element's viewport so the first rows of the
  element are visible for the user.

  By default Unpoly will always reveal an element before
  updating it with JavaScript functions like [`up.replace()`](/up.replace)
  or UJS behavior like [`[up-target]`](/a-up-target).

  \#\#\# How Unpoly finds the viewport

  The viewport (the container that is going to be scrolled)
  is the closest parent of the element that is either:

  - the currently open [modal](/up.modal)
  - an element with the attribute `[up-viewport]`
  - the `<body>` element
  - an element matching the selector you have configured using `up.layout.config.viewports.push('my-custom-selector')`

  \#\#\# Fixed elements obstruction the viewport

  Many applications have a navigation bar fixed to the top or bottom,
  obstructing the view on an element.

  You can make `up.reveal()` aware of these fixed elements
  so it can scroll the viewport far enough so the revealed element is fully visible.
  To make `up.reveal()` aware fixed elements you can either:

  - give the element an attribute [`up-fixed="top"`](/up-fixed-top) or [`up-fixed="bottom"`](up-fixed-bottom)
  - [configure default options](/up.layout.config) for `fixedTop` or `fixedBottom`

  @function up.reveal
  @param {string|Element|jQuery} element
  @param {number} [options.speed]
  @param {string} [options.snap]
  @param {string|Element|jQuery} [options.viewport]
  @param {boolean} [options.top=false]
    Whether to scroll the viewport so that the first element row aligns
    with the top edge of the viewport.
  @return {Promise}
    A promise that fulfills when the element is revealed.
  @stable
  ###
  # TODO: Documentation
  reveal = (elementOrSelector, options = {}) ->
    element = e.get(elementOrSelector)
    console.debug("Instantiating RevealMotion with %o", element)
    motion = new up.RevealMotion(element, options)
    scrollingTracker.startMotion(element, motion, options)

  ###**
  @function up.layout.scrollAfterInsertFragment
  @param {boolean|object} [options.restoreScroll]
  @param {boolean|string|jQuery|Element} [options.reveal]
  @param {boolean|string} [options.reveal]
  @return {Promise}
    A promise that is fulfilled when the scrolling has finished.
  @internal
  ###
  scrollAfterInsertFragment = (selectorOrElement, options) ->
    options = u.options(options)
    console.debug("scrollAfterInsertFragment with opts %o", options)
    element = e.get(selectorOrElement)

    hashOpt = options.hash
    revealOpt = options.reveal
    restoreScrollOpt = options.restoreScroll

    scrollOptions = u.only(options, 'scrollBehavior', 'scrollSpeed')

    if restoreScrollOpt
      # If options.restoreScroll is an object, its keys map viewport selectors
      # to scroll positions. If it is just true, we leave the scrollTops option
      # undefined and let restoreScroll() retrieve previous scrollTops from cache.
      givenTops = u.presence(restoreScrollOpt, u.isObject)
      console.debug('-- restoreScroll()')
      return restoreScroll(around: element, scrollTops: givenTops)

    else if hashOpt && revealOpt == true # hash revealing can be disabled with { reveal: false }
      console.debug('--- revealHash(%o)', hashOpt)
      return revealHash(hashOpt, scrollOptions)

    else if revealOpt
      console.debug('--- revealOp is %o', revealOpt)
      # We allow to pass another element as { reveal } option
      if u.isElement(revealOpt) || u.isJQuery(revealOpt)
        element = e.get(revealOpt)

      # We allow to pass a selector as { reveal } option
      else if u.isString(revealOpt)
        console.debug("--- revealing %o with %o", revealOpt, options.origin)
        selector = up.dom.resolveSelector(revealOpt, options.origin)
        console.debug("--- reveal done")
        element = e.get up.first(selector) # TODO: Remove e.get

      else
        # We reveal the given element

      console.debug('-- final element is %o', element)

      # If selectorOrElement was a CSS selector, don't blow up by calling reveal()
      # with an empty jQuery collection. This might happen if a failed form submission
      # reveals the first validation error message, but the error is shown in an
      # unexpected element.
      if element
        return reveal(element, scrollOptions)

    else
      # If we didn't need to scroll above, just return a resolved promise
      # to fulfill this function's signature.
      return Promise.resolve()

  ###**
  [Reveals](/up.reveal) an element matching the given `#hash` anchor.

  Other than the default behavior found in browsers, `up.revealHash` works with
  [multiple viewports](/up-viewport) and honors [fixed elements](/up-fixed-top) obstructing the user's
  view of the viewport.

  When the page loads initially, this function is automatically called with the hash from
  the current URL.

  If no element matches the given `#hash` anchor, a resolved promise is returned.

  @function up.layout.revealHash
  @return {Promise}
    A promise that is fulfilled when scroll position has changed to match the location hash.
  @experimental
  ###
  revealHash = (hash) ->
    if (hash) && (match = firstHashTarget(hash))
      reveal(match, top: true)
    else
      Promise.resolve()

  viewportSelector = ->
    # On Edge the document viewport can be changed from CSS
    [up.browser.documentViewportSelector(), config.viewports...].join(',')

  ###**
  Returns the viewport for the given element.

  Returns the [document's scrolling element](https://developer.mozilla.org/en-US/docs/Web/API/Document/scrollingElement)
  if no closer viewpoint exists.

  @function up.layout.viewportOf
  @param {string|Element|jQuery} selectorOrElement
  @return {jQuery}
  @internal
  ###
  viewportOf = (selectorOrElement, options = {}) ->
    element = e.get(selectorOrElement)
    e.closest(element, viewportSelector())

  ###**
  Returns a jQuery collection of all the viewports contained within the
  given selector or element.

  @function up.layout.viewportsWithin
  @param {string|Element|jQuery} selectorOrElement
  @return jQuery
  @internal
  ###
  viewportsWithin = (selectorOrElement) ->
    element = e.get(selectorOrElement)
    e.subtree(element, viewportSelector())

  viewportsAround = (selectorOrElement) ->
    element = e.get(selectorOrElement)
    e.list(viewportOf(element), viewportsWithin(element))

  ###**
  Returns a jQuery collection of all the viewports on the screen.

  @function up.layout.viewports
  @internal
  ###
  allViewports = ->
    e.all(viewportSelector())

  scrollTopKey = (viewport) ->
    e.toSelector(viewport)

  ###**
  Returns a hash with scroll positions.

  Each key in the hash is a viewport selector. The corresponding
  value is the viewport's top scroll position:

      up.layout.scrollTops()
      => { '.main': 0, '.sidebar': 73 }

  @function up.layout.scrollTops
  @return Object<string, number>
  @internal
  ###
  scrollTops = ->
    u.mapObject allViewports(), (viewport) ->
      [scrollTopKey(viewport), viewport.scrollTop]

  ###**
  @function up.layout.fixedElements
  @internal
  ###
  fixedElements = (root = document) ->
    queryParts = ['[up-fixed]'].concat(config.fixedTop).concat(config.fixedBottom)
    root.querySelectorAll(queryParts.join(','))

  ###**
  Saves the top scroll positions of all the
  viewports configured in [`up.layout.config.viewports`](/up.layout.config).

  The scroll positions will be associated with the current URL.
  They can later be restored by calling [`up.layout.restoreScroll()`](/up.layout.restoreScroll)
  at the same URL.

  Unpoly automatically saves scroll positions whenever a fragment was updated on the page.

  @function up.layout.saveScroll
  @param {string} [options.url]
  @param {Object<string, number>} [options.tops]
  @experimental
  ###
  saveScroll = (options = {}) ->
    url = u.option(options.url, up.history.url())
    tops = u.option(options.tops, scrollTops())
    lastScrollTops.set(url, tops)

  ###**
  Restores [previously saved](/up.layout.saveScroll) scroll positions of viewports
  viewports configured in [`up.layout.config.viewports`](/up.layout.config).

  Unpoly automatically restores scroll positions when the user presses the back button.
  You can disable this behavior by setting [`up.history.config.restoreScroll = false`](/up.history.config).

  @function up.layout.restoreScroll
  @param {jQuery} [options.around]
    If set, only restores viewports that are either an ancestor
    or descendant of the given element.
  @return {Promise}
    A promise that will be fulfilled once scroll positions have been restored.
  @experimental
  ###
  restoreScroll = (options = {}) ->
    url = up.history.url()
    viewports = if options.around then viewportsAround(options.around) else allViewports()
    scrollTopsForUrl = options.scrollTops || lastScrollTops.get(url) || {}

    up.log.group 'Restoring scroll positions for URL %s to %o', url, scrollTopsForUrl, ->
      allScrollPromises = u.map viewports, (viewport) ->
        key = scrollTopKey(viewport)
        scrollTop = scrollTopsForUrl[key] || 0
        scroll(viewport, scrollTop, duration: 0)

      Promise.all(allScrollPromises)

  ###**
  @internal
  ###
  absolutize = (elementOrSelector, options = {}) ->
    element = e.get(elementOrSelector)
    viewport = up.layout.viewportOf(element)

    viewportRect = viewport.getBoundingClientRect()
    originalRect = element.getBoundingClientRect()

    boundsRect = new up.Rect
      left: originalRect.left - viewportRect.left
      top: originalRect.top - viewportRect.top
      width: originalRect.width
      height: originalRect.height

    # Allow the caller to run code before we start shifting elements around.
    options.afterMeasure?()

    u.writeInlineStyle element,
      # If the element had a layout context before, make sure the
      # ghost will have layout context as well (and vice versa).
      position: if element.style.position == 'static' then 'static' else 'relative'
      top:    'auto' # CSS default
      right:  'auto' # CSS default
      bottom: 'auto' # CSS default
      left:   'auto' # CSS default
      width:  '100%' # stretch to the .up-bounds width we set below
      height: '100%' # stretch to the .up-bounds height we set below

    # Wrap the ghost in another container so its margin can expand
    # freely. If we would position the element directly (old implementation),
    # it would gain a layout context which cannot be crossed by margins.
    bounds = e.createFromSelector('.up-bounds')
    # Insert the bounds object before our element, then move element into it.
    e.insertBefore(element, bounds)
    bounds.appendChild(element)

    moveBounds = (diffX, diffY) ->
      boundsRect.left += diffX
      boundsRect.top += diffY
      u.writeInlineStyle(bounds, boundsRect)

    # Position the bounds initially
    moveBounds(0, 0)

    # In theory, element should not have moved visually. However, element
    # (or a child of element) might collapse its margin against a previous
    # sibling element, and now that it is absolute it does not have the
    # same sibling. So we manually correct element's top position so it aligns
    # with the previous top position.
    newElementRect = element.getBoundingClientRect()
    moveBounds(originalRect.left - newElementRect.left, originalRect.top - newElementRect.top)

    for fixedChild in fixedElements(element)
      u.fixedToAbsolute(fixedChild)

    bounds: bounds
    moveBounds: moveBounds

  ###**
  Marks this element as a scrolling container ("viewport").

  Apply this attribute if your app uses a custom panel layout with fixed positioning
  instead of scrolling `<body>`. As an alternative you can also push a selector
  matching your custom viewport to the [`up.layout.config.viewports`](/up.layout.config) array.

  [`up.reveal()`](/up.reveal) will always try to scroll the viewport closest
  to the element that is being revealed. By default this is the `<body>` element.

  \#\#\# Example

  Here is an example for a layout for an e-mail client, showing a list of e-mails
  on the left side and the e-mail text on the right side:

      .side {
        position: fixed;
        top: 0;
        bottom: 0;
        left: 0;
        width: 100px;
        overflow-y: scroll;
      }

      .main {
        position: fixed;
        top: 0;
        bottom: 0;
        left: 100px;
        right: 0;
        overflow-y: scroll;
      }

  This would be the HTML (notice the `up-viewport` attribute):

      <div class=".side" up-viewport>
        <a href="/emails/5001" up-target=".main">Re: Your invoice</a>
        <a href="/emails/2023" up-target=".main">Quote for services</a>
        <a href="/emails/9002" up-target=".main">Fwd: Room reservation</a>
      </div>

      <div class="main" up-viewport>
        <h1>Re: Your Invoice</h1>
        <p>
          Lorem ipsum dolor sit amet, consetetur sadipscing elitr.
          Stet clita kasd gubergren, no sea takimata sanctus est.
        </p>
      </div>

  @selector [up-viewport]
  @stable
  ###

  ###**
  Marks this element as being fixed to the top edge of the screen
  using `position: fixed`.

  When [following a fragment link](/a-up-target), the viewport is scrolled
  so the targeted element becomes visible. By using this attribute you can make
  Unpoly aware of fixed elements that are obstructing the viewport contents.
  Unpoly will then scroll the viewport far enough that the revealed element is fully visible.

  Instead of using this attribute,
  you can also configure a selector in [`up.layout.config.fixedTop`](/up.layout.config#config.fixedTop).

  \#\#\# Example

      <div class="top-nav" up-fixed="top">...</div>

  @selector [up-fixed=top]
  @stable
  ###

  ###**
  Marks this element as being fixed to the bottom edge of the screen
  using `position: fixed`.

  When [following a fragment link](/a-up-target), the viewport is scrolled
  so the targeted element becomes visible. By using this attribute you can make
  Unpoly aware of fixed elements that are obstructing the viewport contents.
  Unpoly will then scroll the viewport far enough that the revealed element is fully visible.

  Instead of using this attribute,
  you can also configure a selector in [`up.layout.config.fixedBottom`](/up.layout.config#config.fixedBottom).

  \#\#\# Example

      <div class="bottom-nav" up-fixed="bottom">...</div>

  @selector [up-fixed=bottom]
  @stable
  ###


  ###**
  Marks this element as being anchored to the right edge of the screen,
  typically fixed navigation bars.

  Since [modal dialogs](/up.modal) hide the document scroll bar,
  elements anchored to the right appear to jump when the dialog opens or
  closes. Applying this attribute to anchored elements will make Unpoly
  aware of the issue and adjust the `right` property accordingly.

  You should give this attribute to layout elements
  with a CSS of `right: 0` with `position: fixed` or `position:absolute`.

  Instead of giving this attribute to any affected element,
  you can also configure a selector in [`up.layout.config.anchoredRight`](/up.layout.config#config.anchoredRight).

  \#\#\# Example

  Here is the CSS for a navigation bar that is anchored to the top edge of the screen:

      .top-nav {
         position: fixed;
         top: 0;
         left: 0;
         right: 0;
       }

  By adding an `up-anchored="right"` attribute to the element, we can prevent the
  `right` edge from jumping when a [modal dialog](/up.modal) opens or closes:

      <div class="top-nav" up-anchored="right">...</div>

  @selector [up-anchored=right]
  @stable
  ###

  ###**
  @function up.layout.firstHashTarget
  @internal
  ###
  firstHashTarget = (hash) ->
    if hash = pureHash(hash)
      byID = e.attributeSelector('id', hash)
      byName = 'a' + e.attributeSelector('name', hash)
      e.get up.first("#{byID},#{byName}") # TODO: Remove e.get()

  ###**
  Returns `'foo'` if the hash is `'#foo'`.

  Returns undefined if the hash is `'#'`, `''` or `undefined`.

  @function up.browser.hash
  @internal
  ###
  pureHash = (value) ->
    if value && value[0] == '#'
      value = value.substr(1)
    u.presence(value)

  up.on 'up:app:booted', -> revealHash(location.hash)

  up.on 'up:framework:reset', reset

  <% if ENV['JS_KNIFE'] %>knife: eval(Knife.point)<% end %>
  reveal: reveal
  revealHash: revealHash
  firstHashTarget: firstHashTarget
  scroll: scroll
  config: config
  viewportOf: viewportOf
  viewportsWithin: viewportsWithin
  viewports: allViewports
  scrollTops: scrollTops
  saveScroll: saveScroll
  restoreScroll: restoreScroll
  scrollAfterInsertFragment: scrollAfterInsertFragment
  anchoredRight: anchoredRight
  fixedElements: fixedElements
  absolutize: absolutize

up.scroll = up.layout.scroll
up.reveal = up.layout.reveal
up.revealHash = up.layout.revealHash
